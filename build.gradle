// Copyright 2019-2020 LinkedIn Corporation. All rights reserved.
// Licensed under the BSD-2 Clause license.
// See LICENSE in the project root for license information.

buildscript {
  repositories {
    mavenCentral()
    maven {
      url "https://plugins.gradle.org/m2/"
    }
  }
  dependencies {
    classpath "io.github.gradle-nexus:publish-plugin:1.1.0"
    classpath "org.shipkit:shipkit-auto-version:1.1.1"
    classpath "org.shipkit:shipkit-changelog:1.1.10"
  }
}

plugins {
  id "com.diffplug.spotless" version "6.25.0"
}

apply from: "gradle/shipkit.gradle"

allprojects {
  group = "com.linkedin.coral"
  apply plugin: "com.diffplug.spotless"

  // Set Java 17 toolchain for all projects
  plugins.withType(JavaPlugin) {
    java {
      toolchain {
        languageVersion = JavaLanguageVersion.of(17)
      }
    }
  }

  repositories {
    mavenCentral()
    maven {
      url 'https://linkedin.jfrog.io/artifactory/avro-util/'
    }
    maven { url ' https://linkedin.jfrog.io/artifactory/maven/' }
  }

  spotless {
    ratchetFrom 'origin/master'
    groovyGradle {
      target '**/*.gradle'
      endWithNewline()
      greclipse().configFile("${rootDir}/gradle/spotless/codestyle-eclipse.xml")
    }
    format 'markdown', {
      target '**/*.md'
      targetExclude 'docs/release-notes.md'
      endWithNewline()
      // Disabling Prettier since it causes TravisCI to barf
      // prettier()
    }
  }
}

subprojects {
  plugins.withType(JavaPlugin) {
    dependencies {
      testImplementation deps.'testing'
    }
    test {
      useTestNG()
    }
    spotless {
      java {
        importOrder('java', 'javax', 'com', 'org', 'com.linkedin.coral', '\\#')
        removeUnusedImports()
        eclipse().configFile("${rootDir}/gradle/spotless/codestyle-eclipse.xml")
        licenseHeaderFile file("${rootDir}/gradle/license/LICENSE_HEADER")
      }
    }
  }

  apply from: "${rootDir}/gradle/dependencies.gradle"
  apply from: "${rootDir}/gradle/java-publication.gradle"
}
