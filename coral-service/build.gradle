plugins {
  id 'org.springframework.boot' version '3.3.5'
  id 'io.spring.dependency-management' version '1.1.4'
  id 'java'
}

tasks.named('jar') {
  enabled = false
}
tasks.named('bootJar') {
  archiveClassifier = null
}

repositories {
  mavenCentral()
  maven { url " https://conjars.org/repo" }
}

// Exclusions required to fix issue of multiple bindings on the class path
configurations.all {
  exclude module: 'slf4j-log4j12'
  exclude group: 'javax.servlet'
}

dependencies {
  // Coral modules
  implementation project(':coral-common')
  implementation project(':coral-hive')
  implementation project(':coral-incremental')
  implementation project(':coral-trino')
  implementation project(':coral-spark')
  implementation project(':coral-visualization')

  // Calcite APIs used directly in coral-service (RelNode, SqlNode, RelToSqlConverter)
  implementation 'com.linkedin.calcite:calcite-core:1.21.0.265'
  implementation 'com.linkedin.calcite:calcite-linq4j:1.21.0.265'
  // Hive + Hadoop
  implementation('org.apache.hive:hive-exec:2.3.9:core') {
    // prevent old calcite dragged in by hive-exec
    exclude group: 'org.apache.calcite', module: 'calcite-core'
    exclude group: 'org.apache.calcite', module: 'calcite-avatica'
  }
  implementation('org.apache.hive:hive-metastore:2.3.9') {
    exclude group: 'org.apache.calcite', module: 'calcite-core'
    exclude group: 'org.apache.calcite', module: 'calcite-avatica'
  }
  implementation 'org.apache.hadoop:hadoop-common:2.7.0'
  implementation 'org.apache.hadoop:hadoop-mapreduce-client-core:2.7.0'

  // DataNucleus dependencies required by Hive Metastore under JDK 17
  runtimeOnly 'org.datanucleus:datanucleus-api-jdo:4.2.5'
  runtimeOnly 'org.datanucleus:datanucleus-core:4.1.17'
  runtimeOnly 'org.datanucleus:datanucleus-rdbms:4.1.19'
  runtimeOnly 'org.apache.derby:derby:10.10.2.0'

  // Spring Boot
  implementation 'org.springframework.boot:spring-boot-starter-web'
  implementation 'org.springframework.boot:spring-boot-starter-thymeleaf'

  // Misc
  implementation 'com.esotericsoftware.kryo:kryo:2.22'
  implementation(project(path: ':shading:guava-shaded', configuration: 'shadow'))
  implementation 'commons-io:commons-io:2.11.0' // FileUtils (if used)

  // Tests (if you run service unit tests)
  testImplementation 'org.testng:testng:6.4'
}
