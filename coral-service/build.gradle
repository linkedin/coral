plugins {
  id 'org.springframework.boot' version '2.4.0'
  id 'io.spring.dependency-management' version '1.0.11.RELEASE'
  id 'java'
}

// Ref: https://docs.gradle.org/current/userguide/upgrading_version_6.html?_ga=2.55190571.1487781572.1677273009-780026602.1675378719#publishing_spring_boot_applications
configurations {
  [apiElements, runtimeElements].each {
    it.outgoing.artifacts.removeIf { it.buildDependencies.getDependencies(null).contains(jar) }
    it.outgoing.artifact(bootJar)
  }
}

repositories {
  maven { url "http://conjars.org/repo" }
}

// Exclusions required to fix issue of multiple bindings on the class path
configurations.all {
  exclude module: 'slf4j-log4j12'
  exclude group: 'javax.servlet'
}

dependencies {
  implementation project(':coral-common')
  implementation project(':coral-hive')
  implementation project(':coral-incremental')
  implementation project(':coral-trino')
  implementation project(':coral-spark')
  implementation project(':coral-visualization')

  implementation('org.apache.hive:hive-exec:1.2.2:core') {
    exclude group: 'org.apache.calcite', module: 'calcite-core'
  }
  implementation 'org.apache.hadoop:hadoop-mapreduce-client-core:2.7.0'
  implementation 'org.springframework.boot:spring-boot-starter-web'
  implementation 'org.springframework.boot:spring-boot-starter-thymeleaf'
  // Need to add this to avoid class not found issue while setting up local metastore
  implementation 'com.esotericsoftware.kryo:kryo:2.22'
}
