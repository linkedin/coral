plugins {
  id 'org.springframework.boot' version '3.3.5'
  id 'io.spring.dependency-management' version '1.1.4'
  id 'java'
}

tasks.named('jar') {
  enabled = false
}
tasks.named('bootJar') {
  archiveClassifier = null
}

repositories {
  mavenCentral()
  maven { url " https://conjars.org/repo" }
}

// Exclusions required to fix issue of multiple bindings on the class path
configurations.all {
  exclude module: 'slf4j-log4j12'
  exclude group: 'javax.servlet'
}

dependencies {
  implementation project(':coral-common')
  implementation project(':coral-hive')
  implementation project(':coral-incremental')
  implementation project(':coral-trino')
  implementation project(':coral-spark')
  implementation project(':coral-visualization')

  implementation deps.'linkedin-calcite-core'
  implementation deps.'linkedin-calcite-linq4j'
  implementation(deps.'hive'.'hive-exec-core') {
    // prevent old calcite dragged in by hive-exec
    exclude group: 'org.apache.calcite', module: 'calcite-core'
    exclude group: 'org.apache.calcite', module: 'calcite-avatica'
  }
  implementation(deps.'hive'.'hive-metastore') {
    exclude group: 'org.apache.calcite', module: 'calcite-core'
    exclude group: 'org.apache.calcite', module: 'calcite-avatica'
  }
  implementation deps.'hadoop'.'hadoop-common'
  implementation deps.'hadoop'.'hadoop-mapreduce-client-core'

  runtimeOnly deps.'datanucleus-api-jdo'
  runtimeOnly deps.'datanucleus-core'
  runtimeOnly deps.'datanucleus-rdbms'
  runtimeOnly deps.'derby'

  implementation 'org.springframework.boot:spring-boot-starter-web'
  implementation 'org.springframework.boot:spring-boot-starter-thymeleaf'

  implementation deps.'kryo'
  implementation(project(path: ':shading:guava-shaded', configuration: 'shadow'))
  implementation 'commons-io:commons-io:2.11.0'

  testImplementation deps.'testing'
}
