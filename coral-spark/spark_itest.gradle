def itests = ['spark', 'spark3']

def mainSourceSet = sourceSets.main

for (String itest : itests) {
  sourceSets {
    create("${itest}test") {
      java {
        compileClasspath += mainSourceSet.output
        runtimeClasspath += mainSourceSet.output
      }
    }
  }

  configurations.getByName("${itest}testImplementation").extendsFrom(configurations.getByName("testImplementation"))
  configurations.getByName("${itest}testRuntimeOnly").extendsFrom(configurations.getByName("testRuntimeOnly"))

  tasks.register("${itest}Test", Test) {
    description = "Run ${itest} integration tests"
    testClassesDirs = sourceSets["${itest}test"].output.classesDirs
    classpath = sourceSets["${itest}test"].runtimeClasspath
    shouldRunAfter(tasks.test)
    useTestNG()
  }

  tasks.check.configure { dependsOn(tasks.named("${itest}Test")) }
}

dependencies {
  sparktestImplementation deps['spark']['hive']
  spark3testImplementation deps['spark3']['hive']
}
