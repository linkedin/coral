plugins {
  id 'java'
}

repositories {
  maven { url 'https://linkedin.jfrog.io/artifactory/calcite' }
  mavenCentral()
}

dependencies {
  // NOTE: jline is needed as testImplementation for PigUnit tests because it is missing from Pig runtime
  testImplementation deps.'jline'

  implementation(project(path: ':coral-hive')) {
    exclude group: 'jline', module: 'jline'
  }

  testImplementation(deps.'pig'.'pig') { transitive = false }
  testImplementation(deps.'pig'.'pigunit') { transitive = false }

  // Pull in hive-metastore transitively through hive-exec.
  // Use shaded hive-exec/hive-metastore dependencies for Coral to prevent classpath
  // conflicts with DaliStorage/PiggyBank.
  testImplementation(deps.'hive'.'hive-exec-core') {
    exclude group: 'org.apache.avro', module: 'avro-tools'
    // These exclusions are required to prevent duplicate classes since we include
    // shaded coral-calcite jar above
    exclude group: 'org.apache.calcite', module: 'calcite-core'
    exclude group: 'org.apache.calcite', module: 'calcite-avatica'
    exclude group: 'jline', module: 'jline'
    exclude group: 'org.codehaus.groovy', module: 'groovy-all'
  }

  testImplementation deps.'hadoop'.'hadoop-mapreduce-client-common'
  testImplementation deps.'kryo'
  implementation(project(path: ':shading:guava-shaded', configuration: 'shadow'))

  implementation project(path: ':coral-common')
  implementation 'org.apache.commons:commons-lang3:3.12.0'
  implementation 'com.linkedin.calcite:calcite-core:1.21.0.265'
  implementation 'org.apache.calcite:calcite-linq4j:1.21.0'
}
